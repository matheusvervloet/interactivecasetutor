###################################    BOT INTRO      ############################################


topic: ~CASE_TOPIC repeat ()

t: RESET () ^keep() ^eval(:reset)

# t: BEGIN_CASE () ^respond(~FIFTH_PROMPT)
t: BEGIN_CASE () Welcome to your case interview practice, and thank you for being here! I'm $botname and I'll be your guide. \n ^reuse(ASK_NAME)

t: ASK_NAME () What's your name [$repeat , once again] ?
^keep()
	#! Brianne
	a: (_~propername !$repeat )
	^reuse(CHECK_NAME)
	#! My name is vinitius
	a: CHECK_NAME (I name be _*~4) So your name is ^original('_0) , is that right?
		$name = ^original('_0)
		b: (~yes) ^respond(~CASE_INTRO) 
		b: (~no) Could you repeat it for me, please? $repeat = 1 ^reuse(ASK_NAME)
		b: () ^reuse(ASK_NAME)
	#! It's dog $repeat = 1
	a: ($repeat {I} {it} {name} {be} _*) ^reuse(CHECK_NAME)
	a: () I didn't quite get it. $repeat = 1 ^reuse(ASK_NAME)

##################################    CASE INTRO       ###########################################

topic: ~CASE_INTRO  repeat ()

u: () Very nice to meet you, $name! To ensure a pleasant experience in this case interview, there are some basic orientations we should follow. They are: \n ^retry(TOPIC)

u: () 1. Always answer in English. \n 2. Try to respond directly to what was asked. \n 3. Always use a single sentence for your answer (avoid dividing it with commas or "and"). \n 4. Try acting like you would in a real case interview. \n 5. You can always answer "I don't know" if you can't answer a question. \n 6. There is no time limit for this case. \n ^retry(TOPIC)

u: () Also, you can always refresh/reload this webpage if you wish to restart. Are you ready to begin our interview?
	a: (~yes) ^retry(TOPIC)
	a: WAIT_READY (~no) Okay, I can wait! Please say "I'm ready" when you're ready.
		b: (![ no not aint ] ready) ^retry(TOPIC)
		b: () ^reuse(WAIT_READY)
	a: () ^reuse(WAIT_READY)

u: () All right, $name, let's start our case. I'll begin with the introduction. \n ^reuse(CASE_INTRO)

u: CASE_INTRO () Our client is a coffin maker in Moldova, Eastern Europe. His company produces high-quality handmade wooden coffins with skilled employees. \n However, the trend in the business is changing and new technology would allow him to build coffins using machines, which would require much less labor. \n The client wants to know if he should invest in this coffin-making technology or if he should leave the business completely. \n First, let's try to list what our client could do with his company. \n ^respond(~FIRST_PROMPT)

##################################    FIRST PROMPT     ###########################################

concept: ~company_business [business company operation industry everything]
topic: ~FIRST_PROMPT repeat ()

u: FIRST_TEXT () [!$strat_moves Please name a strategic move that our client should consider.] [$strat_moves What else?] [$strat_moves Can you think of something else?] [$strat_moves Any other ideas?] [$strat_moves Could you try something else?]
^keep()
	#! Sell the whole business
	a: (!$strat_1 << sell [~company_business it] >>) [Ok] [Right] [Correct] [That's right] [True]! He could sell the company as a whole. \n
	if(!$strat_1) {$strat_moves += 1}
	$strat_1 = 1
	if($strat_moves<4) {^reuse(FIRST_TEXT)} else {^respond(~FIRST_RESPONSE)}
	
	#! Shut it down
	a: (!$strat_2 [(shut * down) quit close leave liquidate [<< sell assets >>]]) [Ok] [Right] [Correct] [That's right] [True]! He could close the company and sell the assets. \n
	if(!$strat_2) {$strat_moves += 1}
	$strat_2 = 1
	if($strat_moves<4) {^reuse(FIRST_TEXT)} else {^respond(~FIRST_RESPONSE)}
	
	#! The owner put money into the new method
	a: (!$strat_3 [invest "put money"]) ^reuse(STRAT_INVEST)
	#! He should try in the tech
	a: STRAT_INVEST (!$strat_3 ![ no not] [technology tech process method machine investment]) [Ok] [Right] [Correct] [That's right] [True]! He could invest in the new technology. \n
	if(!$strat_3) {$strat_moves += 1}
	$strat_3 = 1
	if($strat_moves<4) {^reuse(FIRST_TEXT)} else {^respond(~FIRST_RESPONSE)}
	
	#! He could do nothing
	a: (!$strat_4 do nothing) ^reuse(STRAT_STILL)
	#! He should stay where he is
	a: STRAT_STILL ([stay keep still same]) [Ok] [Right] [Correct] [That's right] [True]! He could keep the business as it is right now. \n
	if(!$strat_4) {$strat_moves += 1}
	$strat_4 = 1
	if($strat_moves<4) {^reuse(FIRST_TEXT)} else {^respond(~FIRST_RESPONSE)}

	#! I don't know
	a: ([~no ~emoignorance not]) ^respond(~FIRST_RESPONSE)

	a: (<<[he client] can>>) That's a possibility. \n ^reuse(FIRST_TEXT)

	a: () ^reuse(FIRST_TEXT)

topic: ~FIRST_RESPONSE  repeat ()
u: ALL_STRAT_MOVES ($strat_moves>3) [Impressive] [Wow]  , you got them all. \n
^respond(~SECOND_PROMPT)
u: ($strat_moves>1) [Great] [good] [nice] job! \n As you just listed, his possibilities are staying as he is, investing in the new technology, selling the company as a whole or closing the company and selling the assets. \n
^respond(~SECOND_PROMPT)
u: () That's ok! \n For instance, we can consider he could stay as he is, invest in the new technology, sell the company as a whole or close the company and sell the assets. These should be the main options for our client. \n
^respond(~SECOND_PROMPT)

##################################   SECOND PROMPT   ###########################################

concept: ~company_product [~body_container product item unit good]
topic: ~SECOND_PROMPT  repeat ()

u: SECOND_INTRO () Now that we know what steps he could take, let's try to figure out the yearly profit of our client. ^reuse(SECOND_QUESTION)

t: SECOND_QUESTION ([!$size_price !$size_cost !$size_market] !$size_nothing)
if (!$size_price AND !$size_cost AND !$size_competition AND !$size_factors AND !$size_market) {What information do you need to calculate his profit? Please enter one at a time.}
else {[What other information do you need?][What else do you need?]}
^keep()
	#! What is the price of the coffins?
	#! How is the product priced?
	a: SECOND_PRICE (<< price ~company_product >>) The price of the handmade coffin is $5000. \n
	$size_price=1
	^retry(TOPIC)
	#! What's the price
	a: (price) Price of what?
		b: ([~company_product]) ^reuse(SECOND_PRICE)
		b: () Could you please ask again? ^reuse(SECOND_QUESTION)

	#! What's the unit cost?
	a: (<< cost [~company_product ~company_business] >>)  ^reuse(SECOND_COST)
	#! What's the cost?
	a: SECOND_COST (cost) The cost per good sold is of $4800. \n Of that cost, 10% is from materials and the other 90% is labor. \n There is also a $700,000/year fixed cost. \n
	$size_cost=1
	^retry(TOPIC)

	#! Is there competition?
	a: SECOND_COMPETITION ([competition competitor share]) His company has about 10% market share, and is aproximatedly the same size as the nearest competitor. \n
	$size_competition=1
	^retry(TOPIC)
	
	#! Are there any exogenous factors?
	#! What are the trends in the market?
	#! Should we worry about regulation?
	a: SECOND_COMPETITION ([exogenous outside trend regulation other "something else" "anything else"]) You don't need to worry about any other external factor. \n
	$size_factors=1
	^retry(TOPIC)

	#! What is the volume of sales?
	a: ([volume sale transaction revenue income]) ^reuse(SECOND_MARKET)
	#! How many coffins are sold per month?
	a: (<<[sell sale buy trade produce made] ~company_product>>) ^reuse(SECOND_MARKET)
	#! I need to know the size of the market
	a: SECOND_MARKET (<<market size>>) Let's try to estimate the market size. \n Moldova has a population of 4 million people, with a population growth of 0% and a life expectancy of 75 years. \n You can assume a flat age distribution (same number of people at every age), and that about 75% of people are buried in coffins. \n How many coffins are purchased per year in Moldova, then?
	$size_market=1
		b: (_~number _0=40) ^reuse(EXACT_SIZE)
		b: EXACT_SIZE (_~number _0<40100 _0>39900) $exact_size = 1 Perfect! About 40,000 coffins are purchased every year in the whole country. Since he has a 10% market share, he sells about 4,000 coffins every year. \n 
		Now back to the profit. ^retry(TOPIC)
		b: (_~number _0<51 _0>29) ^reuse(CLOSE_SIZE)
		b: CLOSE_SIZE (_~number _0<50001 _0>29999) Pretty close, about 40,000 coffins are purchased every year in the whole country. Since he has a 10% market share, he sells about 4,000 coffins every year. \n 
		^reuse(EXPLAIN_MARKET)
		b: (_~number ) Actually, about 40,000 coffins are purchased every year in the whole country. Since he has a 10% market share, he sells about 4,000 coffins every year. \n 
		^reuse(EXPLAIN_MARKET)
		b: ANSWER_SIZE ([~no ~emoignorance not]) [!$SIZE_DONTKNOW That's ok.] \n With this information, we can estimate that about 40,000 coffins are purchased every year in the whole country. Since he has a 10% market share, he sells about 4,000 coffins every year. \n 
		^reuse(EXPLAIN_MARKET)
		b: () $SIZE_DONTKNOW = 1 ^reuse(ANSWER_SIZE)

	#! No
	a: (_~no ^original(_0)=no) ^reuse(PROFIT_DONTKNOW)

	#! I don't know
	a: PROFIT_DONTKNOW ([~emoignorance ([~no not nothing] know)]) Don't you want to try some more?
		b: (~yes) ^reuse(SECOND_QUESTION)
		b: (~no) $VALUE_DONTKNOW = 1 ^reuse(TELL_INFOS) 
		b: () ^reuse(SECOND_QUESTION)

	#! Nothing
	a: ([~no not nothing]) ^reuse(TELL_INFOS)

	#! What is going on?
	a: () What do you mean, exactly? \n ^reuse(SECOND_QUESTION)

t: EXPLAIN_MARKET (!$exact_size !$VALUE_DONTKNOW) The number of coffins sold can be calculated by (4 million) x (1/75) * (75%) = 40,000. \n Now back to the profit. ^retry(TOPIC)

t: () We should have enough information to calculate the profit now! \n ^reuse(TELL_INFOS)
t: TELL_INFOS () [!$VALUE_DONTKNOW Let's recap.] The cost per good sold is of $4800. Of that cost, 10% is from materials and the other 90% is labor. \n The price of the handmade coffin is $5000. \n About 40,000 coffins are purchased every year in Moldova, and the client has 10% market share. \n
^reuse(ASK_PROFIT)
t: ASK_PROFIT () With that information, what should his yearly profit be?
	a: (_~number _0=100) ^reuse(EXACT_PROFIT)
	a: EXACT_PROFIT (_~number _0<101000 _0>99000) $exact_profit = 1 Great! His profit is of around $100,000. \n
	^reuse(FINISH_SECOND)
	a: (_~number _0<121 _0>79) ^reuse(CLOSE_PROFIT)
	a: CLOSE_PROFIT (_~number _0<120001 _0>79999) You were close, his profit is of around $100,000. \n
	^reuse(EXPLAIN_PROFIT)
	a: (_~number ) Not exactly, his profit is of around $100,000. \n
	^reuse(EXPLAIN_PROFIT)
	a: ANSWER_PROFIT ([~no ~emoignorance not]) [!$PROFIT_DONTKNOW Thats ok.]  We should arrive at a profit of around $100,000. \n
	^reuse(EXPLAIN_PROFIT)
	a: () $PROFIT_DONTKNOW = 1 ^reuse(ANSWER_PROFIT) 

t: EXPLAIN_PROFIT (!$exact_profit) The profit can be calculated by ^"(($5,000 â€“ $4,800) x 40,000 coffins x 10% market share) - $700,000 = $100,000." \n ^reuse(FINISH_SECOND)

t: FINISH_SECOND() Now that we know the profit, assuming a standard discount rate of 10%, we can say the business is worth about $1.0M (the present value given the perpetuity, $100,000 / 0.1), whether he sold it or kept it. \n ^respond(~THIRD_PROMPT)


##################################   THIRD PROMPT   ###########################################

topic: ~THIRD_PROMPT  repeat ()

u: THIRD_INTRO () Let's now try to figure out how much he would make by shutting down and selling the assets. \n ^reuse(THIRD_QUESTION)

t: THIRD_QUESTION () What kind of asset you think is most valuable in the case of his company?
	#! Land in the most valuable asset
	a: ([land estate terrain plot plant factory workshop]) That's right, the most important assets are essentialy land and any improvements on it, since the firm builds coffins by hand. \n
	^retry(TOPIC)
	#! The equipment
	a: ([equipment machinery machine apparatus appliance instrument tool gear]) The value of any equipment can be disconsidered, since the firm builds coffins mainly by hand. \n The most important assets are mainly land and any improvements. \n
	^retry(TOPIC)
	a: () The most important assets are mainly land and any improvements on it, since the firm builds coffins by hand. \n
	^retry(TOPIC)

t: () Let's try to calculate the value of the company's assets. Given that the land and improvements were bought 48 years ago by $100,000, with an average appreciation of 6%/year, what is the current value of the assets?
	a: (_~number _0<1.71 _0>1.49) ^reuse(EXACT_ASSET)
	a: (_~number _0<1710 _0>1499) ^reuse(EXACT_ASSET)
	a: EXACT_ASSET (_~number _0<1700001 _0>1499999) $exact_asset = 1 Exactly, the assets are worth around $1.6M. \n
	^retry(TOPIC)
	a: (_~number _0<2.10 _0>1.19) ^reuse(CLOSE_ASSET)
	a: (_~number _0<2100 _0>1190) ^reuse(CLOSE_ASSET)
	a: CLOSE_ASSET (_~number _0<2100001 _0>1199999) Almost there, the assets are worth around ^"$1.6M". \n
	^retry(TOPIC)
	a: (_~number ) Sorry, but the assets are worth around $1.6M. \n
	^retry(TOPIC)
	a: ANSWER_ASSET ([~no ~emoignorance not]) We should arrive at a value of around $1.6M for the assets. \n
	^retry(TOPIC)
	a: () ^reuse(ANSWER_ASSET)

t: (!$exact_asset) The value of the assets can be calculated by ^"$100K * (1.06)\^48 = ~$1.6M (this can be simplified using the rule of 72, 6% growth doubles investment every 72/6 = 12 years)". \n ^retry(TOPIC)

t: ()  We have already evaluated three possibilities, staying in the business, selling the company and selling the assets. \n ^respond(~FOURTH_PROMPT)

##################################   FOURTH PROMPT   ###########################################

topic: ~FOURTH_PROMPT repeat ()

u: () Finally, let's try to find out what would the profit be if the owner decided to invest in the new technology. \n
^reuse(FOURTH_QUESTION)

t: FOURTH_QUESTION ([!$tech_investment !$tech_savings])
if (!$tech_investment AND !$tech_savings AND !$tech_other) {What information do you think we need to figure this out?}
else {Anything else?}
^keep()
	#! How much will he have to invest?
	a: (<<[cost price value] [technology tech process method machine investment]>>) ^reuse(COST_TECH)
	a: COST_TECH ( [invest spend money]) $tech_investment = 1 It will cost $1.0M to invest in the new technology. \n ^retry(TOPIC)
	
	#! Will there be any cost savings?
	a: ([cost save reduce saving save worker labor employee production]) $tech_savings = 1  While the cost with materials does not change, labor costs are reduced by 50%. \n There is no change in the fixed costs. \n ^retry(TOPIC)

	#! What is the new price?
	a: ([price profit return]) The new price will depend on the costs, and the gross margin will be kept the same for the sake of simplification. \n ^retry(TOPIC)

	#! Is the tech patented
	a: ([patent proprietary exclusive legal copyright trademark]) ^reuse(FOURTH_COMPETITION)
	#! What will the competitors do?
	a: FOURTH_COMPETITION ([rival competition competitor "other companies"]) $tech_other = 1  The patent for the new technology is owned by a private company, who is not offering exclusivity to anyone, which means competitors can also adopt the new tech. \n ^retry(TOPIC)

	#! Is there any impact on the brand
	a: ([brand image customer preference perception]) You can assume the consumer will be indifferent to the change in the process and that there will be no impact in the brand. \n ^retry(TOPIC)

	a: ([~no not nothing]) $NEW_PROFIT_DONTKNOW = 1 ^reuse(ASK_NEW_PROFIT)

	a: (~emoignorance) ^reuse(ASK_NEW_PROFIT)

	a: () [You can ingnore that at the moment.] [You can assume that won't have an impact.] [That won't be necessary right now.] \n ^retry(TOPIC)

t: () I think we should have enough information to calculate the new profit! \n ^reuse(ASK_NEW_PROFIT)
t: ASK_NEW_PROFIT () [!$NEW_PROFIT_DONTKNOW Let's recap.] Using the new techonology reduces labor costs by 50%, with no change in customer perception of the product. \n Since our competitors can also adopt the new technology, prices in the market will drop, following the reduction of costs. \n Let's assume the *gross margin stays the same* as before the investment. If we find out the new yearly profit, we can discover how long it will take to pay the $1.0M initial investment. \n What is the new yearly profit, given the data provided? \n
	a: (_~number _0<271 _0>249) ^reuse(EXACT_NEW_PROFIT)
	a: (_~number _0>-269 _0<-251) ^reuse(EXACT_NEW_PROFIT)
	a: (_~number _0>-269999 _0<-250001) ^reuse(EXACT_NEW_PROFIT)
	a: EXACT_NEW_PROFIT (_~number _0<270001 _0>249999) $exact_new_profit = 1 Exactly! He will have a loss of around $260,000 per year. \n
	^reuse(END_FOURTH)
	a: (_~number _0<301 _0>219) ^reuse(CLOSE_NEW_PROFIT)
	a: (_~number _0>-299 _0<-221) ^reuse(CLOSE_NEW_PROFIT)
	a: (_~number _0>-299999 _0<-220001) ^reuse(CLOSE_NEW_PROFIT)
	a: CLOSE_NEW_PROFIT (_~number _0<300001 _0>21999) You were close, he will have a loss of around $260,000 per year. \n
	^reuse(FINISH_FOURTH)
	a: (_~number ) Not exactly, he will actually have a loss of around $260,000 per year. \n
	^reuse(FINISH_FOURTH)
	a: ANSWER_NEW_PROFIT ([~no ~emoignorance not]) [!$NEW_PROFIT_SOMETHING_ELSE That's ok.] Actually, there will actually be a loss of around $260,000 per year. \n
	^reuse(FINISH_FOURTH)
	a: () $NEW_PROFIT_SOMETHING_ELSE = 1 ^reuse(ANSWER_NEW_PROFIT)

t: FINISH_FOURTH (!$exact_new_profit) The new profit can be calculated by getting to the new price of: ^"(($4,800 * 0.9 * 0.5) labor cost + ($4,800 * 0.1) material cost) / (1 - 0.04) profit margin = $2750." \n ^"Then the profit will be: ($2750 price - $2640 costs) * 4,000 units sold - $700,000 fixed cost = -$260,000" \n ^reuse(END_FOURTH)

t: END_FOURTH () With the reduction in profits, actually a loss, the company will definetly be worth less than it was before investing in the technology, and the investment will never be paid. \n
^respond(~FIFTH_PROMPT)

##################################   FIFTH PROMPT   ###########################################

topic: ~FIFTH_PROMPT repeat ()

u: FINAL_INVEST () All right, that was a lot of work! \n We have now explored all possible strategies for our client, we're close to finishing our case. After all, do you think he should invest in the new technology?
	a: (~yes) Actually, investing in the tech is not such a good idea, since it will severely hurt his profit margins. \n
	^retry(TOPIC)
	a: (~no) Correct, investing in the tech does not make much sense, since it will severely hurt his proft margins. \n
	^retry(TOPIC)
	a: () Our client should not invest in the tech, since it would severely hurt his profit margins. \n
	^retry(TOPIC)

t: FINAL_OFFER () Now, given that our won't invest in the technology, let's say our client receives an offer of $1.0M for the whole business. \n Do you think he should take it?
	a: (~yes) Actually, the business would be a bit undervalued at $1.0M, since we calculated the assets are worth about $1.6M if sold by themselves. \n The owner could look for better offers, or close the company and directly sell the assets. \n
	^retry(TOPIC)
	a: (~no) Good, what do you think he should do, then?
		b: REFUSE_DEAL ([(shut * down) quit close leave liquidate [<< sell assets >>]]) [!$FIFTH_DONTKNOW Yes,] the owner could look for better offers, or shut down and directly sell the assets for around $1.6M. \n
		^retry(TOPIC)
		b: (do nothing) ^reuse(STRAT_STILL)
		b: STRAT_STILL ([stay keep still same]) He could continue to operate the business as is for a while, but that wouldn't be sustainable in the long run due to the dropping costs. \n His end-game should be trying to sell the business or assets. \n
		^retry(TOPIC)
		b: (~emoignorance) $FIFTH_DONTKNOW = 1 ^reuse(REFUSE_DEAL)
		b: () $FIFTH_DONTKNOW = 1 ^reuse(REFUSE_DEAL)

	a: (~emoignorance) $FIFTH_DONTKNOW = 1 ^reuse(REFUSE_DEAL) 
	a: () $FIFTH_DONTKNOW = 1 ^reuse(REFUSE_DEAL)

t: () ^respond(~END_CASE)

####################################   END CASE   ###########################################

topic: ~END_CASE repeat ()

u: () All right! We have analysed the outcomes of all possible strategies and determined the best one to follow, great job! \n Thank you for your dedication and patience, I hope you were able to improve your case-solving skills with this experience. \n
^reuse(FEEDBACK)

t: FEEDBACK ()  My script is still under development, so I'm sorry for any unexpected response. Feedback is greatly apreciated! \n Please comment on your experience on the following link: ^"<a href=https://goo.gl/forms/OP5Q2sroI4W00zWe2>https://goo.gl/forms/OP5Q2sroI4W00zWe2</a>" \n
^respond(~END_LOOP)


###############################################################################################

topic: ~END_LOOP repeat keep ()

t: () We have finished the case. 


# b: (<<[technology tech process method machine] ~company_business [unprofitable loss]>>)
# b: (~yes {he} {should} {take} {it} {offer} {the} {do} >) Can you explain why?
